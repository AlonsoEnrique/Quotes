CMAKE_MINIMUM_REQUIRED (VERSION 3.0)

PROJECT (com.github.alonsoenrique.quotes)

LIST (APPEND CMAKE_MODULE_PATH ${CMAKE_SOURCE_DIR}/cmake)
LIST (APPEND CMAKE_MODULE_PATH ${CMAKE_SOURCE_DIR}/cmake/src)

SET (DATADIR "${CMAKE_INSTALL_PREFIX}/share")
SET (APP_ID "com.github.alonsoenrique.quotes")
SET (EXEC_NAME "com.github.alonsoenrique.quotes")
SET (RELEASE_NAME "Quotes for daily inspiration")
SET (VERSION "0.5.6")
SET (VERSION_INFO "Release")
SET (LIBNAME lib-${EXEC_NAME})
# the path where library files should be searched for
SET (LIBRARY_PATH "src")

FIND_PACKAGE (PkgConfig)
PKG_CHECK_MODULES (DEPS REQUIRED gtk+-3.0 libsoup-2.4 json-glib-1.0)

SET(APPLICATION_DEPS
    gtk+-3.0>=3.12
    libsoup-2.4
    json-glib-1.0
)

ADD_DEFINITIONS (${DEPS_CFLAGS})
LINK_LIBRARIES (${DEPS_LIBRARIES})
LINK_DIRECTORIES (${DEPS_LIBRARIES_DIRS})

FIND_PACKAGE (Vala REQUIRED)
INCLUDE (ValaVersion)
ENSURE_VALA_VERSION ("0.26" MINIMUM)

# compile library from src folder
ADD_SUBDIRECTORY (src)

INCLUDE (ValaPrecompile)
VALA_PRECOMPILE (VALA_C ${EXEC_NAME}
    src/Main.vala

PACKAGES
    ${APPLICATION_DEPS}

CUSTOM_VAPIS
    ${CMAKE_BINARY_DIR}/${LIBRARY_PATH}/${LIBNAME}_internal.vapi
)

INCLUDE (GResource)
GLIB_COMPILE_RESOURCES (GLIB_RESOURCES_ICONS SOURCE data/images/icons/icons.gresource.xml)
GLIB_COMPILE_RESOURCES (GLIB_RESOURCES_CSS SOURCE data/css/css.gresource.xml)

INCLUDE (GSettings)
ADD_SCHEMA ("data/com.github.alonsoenrique.quotes.gschema.xml")

# tell cmake what to call the executable we just made
ADD_EXECUTABLE (${EXEC_NAME} ${VALA_C} ${GLIB_RESOURCES_ICONS} ${GLIB_RESOURCES_CSS})
ADD_DEPENDENCIES (${EXEC_NAME} ${LIBNAME})
TARGET_LINK_LIBRARIES (${EXEC_NAME} ${DEPS_LIBRARIES} ${LIBNAME})
SET_TARGET_PROPERTIES (${EXEC_NAME} PROPERTIES
    INCLUDE_DIRECTORIES ${CMAKE_BINARY_DIR}/${LIBRARY_PATH}
)

INSTALL (TARGETS ${EXEC_NAME} RUNTIME DESTINATION bin)

INSTALL (FILES ${CMAKE_CURRENT_SOURCE_DIR}/data/${EXEC_NAME}.desktop DESTINATION ${DATADIR}/applications/)
INSTALL (FILES ${CMAKE_CURRENT_SOURCE_DIR}/data/${EXEC_NAME}.appdata.xml DESTINATION ${DATADIR}/metainfo/)

SET (ICONS_SIZE 16;24;32;48;64;128)

FOREACH (ICON_SIZE ${ICONS_SIZE})
    INSTALL (FILES ${CMAKE_CURRENT_SOURCE_DIR}/data/images/icons/${ICON_SIZE}/${EXEC_NAME}.svg DESTINATION ${DATADIR}/icons/hicolor/${ICON_SIZE}x${ICON_SIZE}/apps/)
ENDFOREACH ()

OPTION (BUILD_TESTS "Build a unit test executable." OFF)

if (BUILD_TESTS)
    ADD_SUBDIRECTORY (tests)
endif (BUILD_TESTS)
